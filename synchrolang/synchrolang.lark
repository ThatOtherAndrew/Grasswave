%import common.DIGIT
%import common.SIGNED_INT
%import common.SIGNED_FLOAT
%import common.ESCAPED_STRING
%import common.LETTER
%import common.CNAME
%import python.atom

%import common.WS
%ignore WS


UPPERCASE_NAME: "A".."Z" ("_" | LETTER | DIGIT)*
LOWERCASE_NAME: "a".."z" ("_" | LETTER | DIGIT)*

?value: ESCAPED_STRING             -> string
      | SIGNED_INT                 -> int
      | SIGNED_FLOAT               -> float
      | "[" (value ","?)* "]"      -> list
      | ("True"i | "False"i)       -> bool
      | ("None"i | "null"i | "_")  -> none

node_class: UPPERCASE_NAME
arguments: (value ","?)*
keyword_arguments: (CNAME "=" value ","?)*
node_init: LOWERCASE_NAME "=" node_class "(" arguments keyword_arguments ")"

node: LOWERCASE_NAME
port: node "." CNAME
input: port
output: port
connection: output "->" input

command: ["create" | "new" | "let"] node_init                             -> create
       | ("link" | "connect" | "attach") connection                       -> link
       | ("unlink" | "disconnect" | "detach") (node | port | connection)  -> unlink
       | ("remove" | "delete") node                                       -> remove

?expression: [node_class | node | port | connection | command]

?start: expression
